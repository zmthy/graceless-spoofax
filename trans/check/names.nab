module check/names

imports
  src-gen/signatures/Graceless-sig

namespaces Type Method Variable

binding rules

  Module(_, methods, _) :
    implicitly defines Variable Self() of type Interface(mtypes)
      where methods has type mtypes
    scopes Type, Method, Variable

  TypeDecl(name, value) :
    defines Type name of type ttype
      where value has type ttype

  Method(_, _) :
    scopes Variable

  Def(name, dtype, _) :
    defines Variable name of type dtype'
      where dtype has type dtype'

  Object(methods) :
    implicitly defines Variable Self() of type Interface(mtypes)
      where methods has type mtypes
    scopes Method, Variable

  Request(receiver, name, _) :
    refers to Method name in rtype
      where receiver has type rtype

  Variable(name) :
    refers to Variable name

  Self() :
    refers to Variable Self()

  TypeRef(name) :
    refers to Type name

  Interface(_) :
    scopes Method

  Signature(name, params, otype) :
    defines Method name of type Signature(name, ptypes, otype')
      where params has type ptypes
        and otype has type otype'

  Parameter(name, ptype) :
    defines Variable name of type ptype'
      where ptype has type ptype'
