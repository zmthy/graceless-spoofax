module Graceless

imports
  Common

context-free start-symbols
  Program

context-free syntax
  Program.Module = [[TypeDecl*] [Method*] [Expression]]

  TypeDecl.TypeDecl = [type [ID] = [Type]]

  Method.Method = [method [Signature] { [Body] }]

  Body.Body = [[Statement*] [Expression]]

  Statement.Def = [def [ID] : [Type] = [Expression];]
  Statement.Var = [var [ID] : [Type] := [Expression];]
  Statement.Assign = [[ID] := [Expression];]

  Expression.Object = [object { [Method*] }]
  Expression.Request = [[Expression].[ID]([{Expression ","}*])]
  Expression.Variable = ID
  Expression.Self = [self]

  Type.TypeRef = ID
  Type.Interface = [type { [Signature*] }]

  Signature.Signature = [[ID]([{Parameter ","}*]) -> [Type]]

  Parameter.Parameter = [[ID] : [Type]]

// Only used in the type checker, not part of the concrete syntax.
context-free syntax
  VariableType.VariableType = [[Type] [VariableKind]]

  VariableKind.Var = []
  VariableKind.Def = []
